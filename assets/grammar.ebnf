Program         = { Decl };
Decl            = ObjDecl | FnDecl | VarDecl | Stmt;

ObjDecl         = "object", ID, [ ObjDeclExtends ], Block;
ObjDeclExtends  = "extends", ID;

FnDecl          = "fn", ID, "(", { FnParam }, ")", Block;
FnParam          = ID, [ "," ];

VarDecl         = "let", ID, [ VarDeclInit ], ";";
VarDeclInit     = "=", Expr;

Stmt            = ExprStmt | ForStmt | IfStmt | PrintStmt | ReturnStmt | WhileStmt | Block;

ExprStmt        = Expr, ";";

ForStmt         = "for", "(", [ ForInit ], [ ForCond ], [ ForIncr ], ")", Stmt;
ForInit         = VarDecl | ExprStmt | ";";
ForCond         = ExprStmt | ";";
ForIncr         = ExprStmt | ";";

IfStmt          = "if", "(", IfCond, ")", Stmt, [ IfElseBranch ];
IfCond          = Expr;
IfMainBranch    = Stmt;
IfElseBranch    = "else", Stmt;

PrintStmt       = "print", "(", Expr, ")", ";";
ReturnStmt      = "return", [ Expr ], ";";
WhileStmt       = "while", "(", Expr, ")", Stmt;
Block           = "{", { Decl }, "}";

Expr            = Assign;

Assign          = LogicOr, [ AssignExpr ];
AssignExpr      = "=", Assign;

LogicOr         = LogicAnd, { LogicOrExpr };
LogicOrExpr     = "or", LogicAnd;

LogicAnd        = Equality, { LogicAndExpr };
LogicAndExpr    = "and", Equality;

Equality        = Comparison, { EqualityExpr };
EqualityExpr    = "!=" | "==", Comparison;

Comparison      = Term, { ComparisonExpr };
ComparisonExpr  = ">" | ">=" | "<" | "<=", Term;

Term            = Factor, { TermExpr };
TermExpr        = "-" | "+", Factor;

Factor          = Unary, { FactorExpr };
FactorExpr      = "/" | "*", Unary;

Unary           = UnaryExpr | Call;
UnaryExpr       = "!" | "-", Unary;

Call            = Primary, { CallExpr };
CallExpr        = CallFn | CallProperty;
CallFn          = "(", { FnArg }, ")";
CallProperty    = ".", ID;
FnArg           = Expr, [ "," ];

Primary         = "true"
                | "false"
                | "nil"
                | "this"
                | "proto"
                | NUMBER
                | STRING
                | ID
                | GroupExpr;

GroupExpr       = "(", Expr, ")";

ALPHA           = "A" .. "Z" | "a" .. "z" | "_";
DIGIT           = "0" .. "9";
NUMBER          = DIGIT, { DIGIT }, [ ".", DIGIT, { DIGIT } ];
STRING          = '"', { ALPHA | DIGIT }, '"';
ID              = ALPHA, { ALPHA | DIGIT };
